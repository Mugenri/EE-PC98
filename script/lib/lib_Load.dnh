//Script/Texture Loading and Unloading Library

#include "./loadlib/lib_LoadStage.dnh"
#include "./lib_Spell.dnh" //spell path lists

function<void> Load_LoadTextureBase() { //load universal textures (generic textures used in all stages)
    for each (img in Load_GetTextureBase()) {
        LoadTexture(img);
        yield;
    }
}

function<string[]> Load_GetTextureBase() {
    return [
        //effect
        IMG_EFFECT_CANCEL,
        IMG_EFFECT_FADE,
        IMG_EFFECT_PARTICLE,
        IMG_EFFECT_SPAWN,
        IMG_EFFECT_TRANSITION,
        //system
        IMG_SYSTEM_FRAME,
        IMG_SYSTEM_ITEM,
        IMG_SYSTEM_LOGO,
        IMG_SYSTEM_SHOT,
        IMG_SYSTEM_UI,
        //player
        IMG_PLR_HITBOX
    ];
}

function<void> Load_LoadTextureTitle() { //load textures used in the title screen
    for each (img in Load_GetTextureTitle()) {
        LoadTexture(img);
        yield;
    }
}

function<void> Load_UnloadTextureTitle() { //unload textures used in the title screen
    for each (img in Load_GetTextureTitle()) {
        RemoveTexture(img);
        yield;
    }
}

function<string[]> Load_GetTextureTitle() { //list of title textures
    return [
        IMG_TT_BG,
        IMG_TT_RANK,
        IMG_TT_PLAYER,
        IMG_TT_SPELL,
        IMG_TT_REPLAY,
        IMG_TT_RESULT,
        IMG_TT_MUSIC,
        IMG_TT_CURSOR
    ];
}

function<void> Load_LoadTextureEnding(int playerType_) { //load textures used in the endings
    for each (img in Load_GetTextureEnding(playerType_)) {
        LoadTexture(img);
        yield;
    }    
}

function<void> Load_UnloadTextureEnding(int playerType_) { //unload textures used in the endings
    for each (img in Load_GetTextureEnding(playerType_)) {
        RemoveTexture(img);
        yield;
    }
}

function<string[]> Load_GetTextureEnding(int playerType_) {
    string[] texture = [IMG_END_TEXTBOX];

    alternative (playerType_)
    case (PLAYER_YABUSAME) {
        texture ~= [
            IMG_END_YABUSAME_BG,
            IMG_END_YABUSAME
        ];
    }
    case (PLAYER_TSUBAKURA) {
        texture ~= [
            //IMG_END_TSUBAKURA
        ];
    }

    //credits textures
    texture ~= [
        IMG_END_STAFFROLL,
        IMG_END_EFFECT
    ];

    return texture;
}