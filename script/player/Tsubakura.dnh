#TouhouDanmakufu[Player]
#ScriptVersion[3]
#Title["Tsubakura Enraku"]
#ID["Tsubakura"]
#Text["Loafing Monochrome"]
#ReplayName["Tsubakura"]

#include "./lib/lib_player.dnh"

int _count = 0;
int _countShot = 0;

@Initialize {
    string imageFile = IMG_PLR_TSUBAKURA;
    string shotData = DIR_PLR ~ "Tsubashot.dnh";
    InitPlayer(imageFile, shotData, 4.5, 2.2);

    TShot();
}

@MainLoop {
    _count++;
    if (Shooting() && !IsPlayerSpellActive() && Alive()) _countShot++;
    else _countShot = -1;
    yield;
}

@Event
{
	PlayerEvent();
}

@Finalize {}

task TShot() {
    TCreateLaser();
    ascent (i in 0..13) TCreateLaserEffect(-i*32);
    ascent (i in 0..6) TOption(i);
    loop {
        if (Shooting() && Alive() && !IsPlayerSpellActive() && _countShot % 10 == 0) {
            loop (3) {
                CallSFX(SND_PL_SHOT);
                loop (3) yield;
            }
        }
        yield;
    }

    task TCreateLaser() { //Hitbox
        int objLaser = ObjShot_Create(OBJ_STRAIGHT_LASER);
        ObjShot_SetGraphic(objLaser, 1);
        ObjShot_SetDamage(objLaser, 4);
        ObjLaser_SetIntersectionWidth(objLaser, 2);
        ObjLaser_SetLength(objLaser, 600);
        ObjShot_SetAutoDelete(objLaser, false);
        ObjLaser_SetInvalidLength(objLaser, 0, 0);
        ObjStLaser_SetAngle(objLaser, 270);
        ObjShot_Regist(objLaser);

        while (!Obj_IsDeleted(_objPlayer)) {
            ObjMove_SetPosition(objLaser, GetPlayerX(), GetPlayerY());
            ObjShot_SetIntersectionEnable(objLaser, Shooting() && !IsPlayerSpellActive() && Alive());
            yield;
        }
        Obj_Delete(objLaser);
    }

    task TCreateLaserEffect(int yOff_) { //Graphic
        int objEffect = ObjPrim_Create(OBJ_SPRITE_2D);
        ObjPrim_SetTexture(objEffect, _pathImg);
        ObjRender_SetPosition(objEffect, GetPlayerX(), GetPlayerY() + yOff_ - 16, 1);
        Obj_SetRenderPriorityI(objEffect, RP_STG_PLAYER - 1i);

        int frame = 0;

        while (!Obj_IsDeleted(_objPlayer)) {
            ObjRender_SetPosition(objEffect, GetPlayerX(), GetPlayerY() + yOff_ - 16, 1);
            ObjSprite2D_SetSourceRect(objEffect, 160, frame*4, 176, frame*4+32);
            ObjSprite2D_SetDestCenter(objEffect);

            Obj_SetVisible(objEffect, Shooting() && !IsPlayerSpellActive() && Alive());

            frame++;
            if (frame >= 8) frame = 0;
            yield;
        }
        Obj_Delete(objEffect);
    }
}


task TOption(int offset_) {
    int distUnfocus = 30;
    int distFocus = 18;
    int distCurrent = distUnfocus;

    int objOption = ObjPrim_Create(OBJ_SPRITE_2D);
    ObjPrim_SetTexture(objOption, _pathImg);
    ObjSprite2D_SetSourceRect(objOption, 179, 3, 189, 13);
    ObjSprite2D_SetDestCenter(objOption);
    Obj_SetRenderPriorityI(objOption, RP_STG_PLAYER - 1i);

    TOptionShot();

    while (!Obj_IsDeleted(_objPlayer)) {
        real rotationX = cos(_count*4 + 360/6*offset_);
        distCurrent = (GetVirtualKeyState(VK_SLOWMOVE) != KEY_HOLD && GetVirtualKeyState(VK_SLOWMOVE) != KEY_PUSH) ? distUnfocus : distFocus;

        ObjRender_SetPosition(objOption, GetPlayerX() + distCurrent*rotationX, GetPlayerY() + distCurrent*sin(_count*3 + 360/6*offset_)*0.8, 1);

        //Render over player as if rotating around them
        if (rotationX == 1) Obj_SetRenderPriorityI(objOption, RP_STG_PLAYER - 1i);
        else if (rotationX == -1) Obj_SetRenderPriorityI(objOption, RP_STG_PLAYER);

        Obj_SetVisible(objOption, Alive());
        yield;
    }

    task TOptionShot() {
        while (!Obj_IsDeleted(_objPlayer)) {
            if (Shooting() && Alive() && !IsPlayerSpellActive() && _countShot % 10 == 0) {
                loop (3) {
                    int shot = CreatePlayerShotA1(ObjRender_GetX(objOption), ObjRender_GetY(objOption), 28, 270, 0.5, 1, 1);
                    Obj_SetRenderPriorityI(shot, RP_STG_PLAYER);
                    loop (3) yield;
                }
            }
            yield;
        }
    }
}

task TSpell() {
    int objManage = GetSpellManageObject();

    SetForbidPlayerShot(true);
    SetPlayerInvincibilityFrame(310);
    ObjRender_SetColor(_objPlayer, COLOR_WHITE);
    SetPlayerSpeed(2.2, 2.2);

    CallSFX(SND_PL_T_SPELL1);
    CallSFX(SND_PL_T_SPELL2);

    TEffect();
    
    //spell time
    wait(250);

    SetForbidPlayerSpell(true);
    SetForbidPlayerShot(false);
    SetPlayerSpeed(4.5, 2.2);
    Obj_Delete(objManage);
    wait(40);

    //vulnerability indicator
    CallSFX(SND_PL_VULNERABLE);
    real radius = 28;
    int circle = ObjPrim_Create(OBJ_PRIMITIVE_2D);
    ObjPrim_SetPrimitiveType(circle, PRIMITIVE_LINESTRIP);
    ObjPrim_SetVertexCount(circle, 17);
    ascent (i in 0..17) ObjPrim_SetVertexColor(circle, i, 170, 221, 255);
    loop (20) {
        ascent (i in 0..17) {
            real angleT = 360/16 * i;
            ObjPrim_SetVertexPosition(circle, i, GetPlayerX() + radius*cos(angleT), GetPlayerY() + radius*sin(angleT), 1);
        }
        radius -= 28/20;
        yield;
    }

    Obj_Delete(circle);
    SetForbidPlayerSpell(false);

    task TEffect() {
        real radius = 0;
        int wCircle = CreateCircle(COLOR_WHITE);
        int bCircle = CreateCircle(COLOR_BLACK);

        //Expand Black Circle
        loop (70) {
            radius += 600/70;
            ObjPrim_SetVertexPosition(bCircle, 0, GetPlayerX(), GetPlayerY(), 1);
            ascent (i in 1..34) {
                real angleT = 360/32 * i;
                ObjPrim_SetVertexPosition(bCircle, i, GetPlayerX() + radius*cos(angleT), GetPlayerY() + radius*sin(angleT), 1);
            }
            DeleteShotInCircle(TYPE_SHOT, TYPE_IMMEDIATE, GetPlayerX(), GetPlayerY(), radius);
            yield;
        }

        //Set White Circle to fill screen behind black circle
        ascent (i in 1..34) {
            real angleT = 360/32 * i;
            ObjPrim_SetVertexPosition(wCircle, i, GetPlayerX() + radius*cos(angleT), GetPlayerY() + radius*sin(angleT), 1);
        }

        loop (30) {
            DeleteShotAll(TYPE_SHOT, TYPE_IMMEDIATE);
            yield;
        }

        //Retract Black Circle
        CallSFX(SND_PL_T_SPELL3);
        loop (45) {
            ObjPrim_SetVertexPosition(bCircle, 0, GetPlayerX(), GetPlayerY(), 1);
            ascent (i in 1..34) {
                real angleT = 360/32 * i;
                ObjPrim_SetVertexPosition(bCircle, i, GetPlayerX() + radius*cos(angleT), GetPlayerY() + radius*sin(angleT), 1);
            }
            DeleteShotAll(TYPE_SHOT, TYPE_IMMEDIATE);
            radius -= 600/45;
            yield;
        }
        Obj_Delete(bCircle);

        //Retract White Circle
        radius = 600;
        CallSFX(SND_PL_T_SPELL3);
        loop (35) {
            ObjPrim_SetVertexPosition(wCircle, 0, GetPlayerX(), GetPlayerY(), 1);
            ascent (i in 1..34) {
                real angleT = 360/32 * i;
                ObjPrim_SetVertexPosition(wCircle, i, GetPlayerX() + radius*cos(angleT), GetPlayerY() + radius*sin(angleT), 1);
            }
            DeleteShotAll(TYPE_SHOT, TYPE_IMMEDIATE);
            radius -= 600/35;
            yield;
        }
        Obj_Delete(wCircle);

        //monochrome ray
        CallSFX(SND_PL_T_SPELL4);

        int laser = ObjSpell_Create();
        ObjSpell_SetEraseShot(laser, false);
        ObjSpell_SetDamage(laser, 45);
        ObjSpell_Regist(laser);

        real hitboxLength = 0;
        real modifier = 0;
        loop (60) {
            TCreateLaserEffect(1);
            TCreateLaserEffect(-1);
            modifier += 360/14;
            ObjSpell_SetIntersectionLine(laser, GetPlayerX(), GetPlayerY(), GetPlayerX(), GetPlayerY() - hitboxLength, 12);
            DeleteShotAll(TYPE_SHOT, TYPE_IMMEDIATE);
            hitboxLength += 28; //bullet speed
            yield;
        }
        Obj_Delete(laser);

        function<int> CreateCircle(int color_) {
            int circle = ObjSpell_Create();
            ObjSpell_SetDamage(circle, 0);
            ObjSpell_SetEraseShot(circle, false);
            ObjPrim_SetPrimitiveType(circle, PRIMITIVE_TRIANGLEFAN);
            ObjPrim_SetVertexCount(circle, 34);
            Obj_SetRenderPriorityI(circle, RP_STG_EFF_PLR);
            ObjPrim_SetVertexPosition(circle, 0, GetPlayerX(), GetPlayerY(), 1);
            ascent (i in 0..34) ObjPrim_SetVertexColor(circle, i, color_);
            ascent (i in 1..34) {
                real angleT = 360/32 * i;
                ObjPrim_SetVertexPosition(circle, i, GetPlayerX() + radius * cos(angleT), GetPlayerY() + radius * sin(angleT), 1);
            }
            ObjSpell_Regist(circle);
            return circle;
        }

        task TCreateLaserEffect(int direction_) {
            int shot1 = ObjPrim_Create(OBJ_SPRITE_2D); //black bullet
            ObjPrim_SetTexture(shot1, _pathImg);
            ObjSprite2D_SetSourceRect(shot1, 144, 0, 160, 16);
            ObjSprite2D_SetDestCenter(shot1);
            Obj_SetRenderPriorityI(shot1, RP_STG_PLAYER - 2i);

            int shot2 = ObjPrim_Create(OBJ_SPRITE_2D); //white bullet
            ObjPrim_SetTexture(shot2, _pathImg);
            ObjSprite2D_SetSourceRect(shot2, 144, 16, 160, 32);
            ObjSprite2D_SetDestCenter(shot2);
            Obj_SetRenderPriorityI(shot2, RP_STG_PLAYER - 2i);

            ObjRender_SetPosition(shot1, GetPlayerX() + 20*direction_*cos(modifier), GetPlayerY() + direction_*4, 1);
            ObjRender_SetPosition(shot2, GetPlayerX() + 15*direction_*cos(modifier + 90), GetPlayerY() + direction_*8, 1);
            loop (20) {
                ObjRender_SetY(shot1, ObjRender_GetY(shot1) - 28); //bullet speed
                ObjRender_SetY(shot2, ObjRender_GetY(shot2) - 28);
                yield;
            }
            Obj_Delete(shot1);
            Obj_Delete(shot2);
        }
    }
}