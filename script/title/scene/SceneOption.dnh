//Options Menu
function<void> SceneOption() {
    //Menu Indices
    const int INDEX_VOLUME_BGM = 0;
    const int INDEX_VOLUME_SFX = 1;
    const int INDEX_STARTING_LIVES = 2;
    
    const int INDEX_DEFAULT = 3;
    const int INDEX_QUIT = 4;

    bool exitFlag = false;

    int selectMax = INDEX_QUIT;
    int indexCurrent = 0;

    TCreateMenuGraphicsOption();

    //Set up the menu
    string[] commonName = ["BGM_Volume", "SFX_Volume", "StartingLives"]; //common data field names
    {
        int menuX = 504;
        int menuY = 24;
        int yOffset = 15;
        string[] texts = ["BGM Volume    ", "SFX Volume    ", "Lives  ", "Default", "Quit"]; //volumes given more space to accomodate a space and 3 extra characters (volume numbers)
        ascent (i in 0..selectMax + 1) {
            TCreateMenuItemOption(i, menuX, menuY, 0, yOffset, texts[i]);
            menuY += yOffset;
        }
    }

    //no fade from previous menu, so no wait time
    WaitVirtualKeysFree([VK_UP, VK_LEFT, VK_DOWN, VK_RIGHT, VK_OK, VK_CANCEL, VK_PAUSE]);
    loop { //Wait for input loop
        int userKey = CheckVirtualKeyPressed([VK_UP, VK_LEFT, VK_DOWN, VK_RIGHT, VK_OK, VK_CANCEL, VK_PAUSE]);
        alternative (userKey)
        case (VK_UP) {
            CallSFX(SND_TT_SELECT);
            indexCurrent = IncrementIndex(indexCurrent, -1, 0, selectMax);
        }
        case (VK_DOWN) {
            CallSFX(SND_TT_SELECT);
            indexCurrent = IncrementIndex(indexCurrent, 1, 0, selectMax);
        }
        case (VK_LEFT) {
            if (indexCurrent == INDEX_VOLUME_BGM || indexCurrent == INDEX_VOLUME_SFX) {
                IncrementValue(indexCurrent, -5, 0, 100);
                CallSFX(SND_TT_SELECT);
            } else if (indexCurrent == INDEX_STARTING_LIVES) {
                IncrementValue(indexCurrent, -1, 0, 7);
                CallSFX(SND_TT_SELECT);
            }
        }
        case (VK_RIGHT) {
            if (indexCurrent == INDEX_VOLUME_BGM || indexCurrent == INDEX_VOLUME_SFX) {
                IncrementValue(indexCurrent, 5, 0, 100);
                CallSFX(SND_TT_SELECT);
            } else if (indexCurrent == INDEX_STARTING_LIVES) {
                IncrementValue(indexCurrent, 1, 0, 7);
                CallSFX(SND_TT_SELECT);
            }
        }
        case (VK_OK) {
            MenuSelect();
            if (indexCurrent == INDEX_DEFAULT) CD_LoadConfig(true);
            if (indexCurrent == INDEX_QUIT) {
                SaveConfig();
                break;
            }
        }
        case (VK_CANCEL, VK_PAUSE) {
            CallSFX(SND_TT_CANCEL);
            indexCurrent = INDEX_QUIT;
        }
    }
    exitFlag = true;

    function<void> MenuSelect() {
        CallSFX(SND_TT_DECIDE);
    }

    task TCreateMenuGraphicsOption() {
        int bg = InitSprite2D(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, IMG_TT_BG, RP_UI_BG);
        int logo = InitSprite2D(640, 0, 967, 87, IMG_TT_BG, RP_UI_TEXT);

        ObjRender_SetPosition(logo, 5, 3, 1);

        while (!exitFlag) yield;
        Obj_Delete(bg);
        Obj_Delete(logo);
    }

    task TCreateMenuItemOption(int index_, int x_, int y_, int xOff_, int yOff_, string text_) {
        int objText = CreateText(x_ + xOff_, y_ + yOff_, text_, COLOR_INDIGO_L);
        int objCursor = CreateCursor(ObjRender_GetX(objText) + (ObjText_GetTotalWidth(objText) / 2), ObjRender_GetY(objText) + 8);
        //Align to middle
        ObjRender_SetX(objText, ObjRender_GetX(objText) - ObjText_GetTotalWidth(objText) / 2);
        ObjRender_SetX(objCursor, ObjRender_GetX(objCursor) - ObjText_GetTotalWidth(objText) / 2);

        //option types
        if (index_ == INDEX_VOLUME_BGM || index_ == INDEX_VOLUME_SFX) TCreateOptionSlider(index_, 100);
        else if (index_ == INDEX_STARTING_LIVES) TCreateOptionSlider(index_, 2);

        while (!exitFlag) {
            ObjText_SetVertexColor(objText, 0xFF000000 | (index_ == indexCurrent ? COLOR_BLACK : COLOR_INDIGO_L));
            Obj_SetVisible(objCursor, index_ == indexCurrent);
            yield;
        }
        Obj_Delete(objText);
        Obj_Delete(objCursor);

        task TCreateOptionSlider(int index_, int default_) {
            int objSlider = CreateText(x_ + 4 + ObjText_GetTotalWidth(objText) / 4, y_ + yOff_, "000", COLOR_INDIGO_L);
            if (GetAreaCommonData(AREA_CONFIG, commonName[index_], default_) == default_) SetAreaCommonData(AREA_CONFIG, commonName[index_], default_);
            
            while (!exitFlag) {
                ObjText_SetText(objSlider, IntToString(GetAreaCommonData(AREA_CONFIG, commonName[index_], default_)));
                ObjText_SetVertexColor(objSlider, 0xFF000000 | (index_ == indexCurrent ? COLOR_BLACK : COLOR_INDIGO_L));
                yield;
            }
            Obj_Delete(objSlider);
        }
    }

    function<void> IncrementValue(int index_, int value_, int min_, int max_) {
        int newValue = clamp(GetAreaCommonData(AREA_CONFIG, commonName[index_], max_) + value_, min_, max_);
        SetAreaCommonData(AREA_CONFIG, commonName[index_], newValue);
    }

    function<void> SaveConfig() {
        if (IsCommonDataAreaExists("SAVED_CONFIG")) { ClearAreaCommonData("SAVED_CONFIG"); }
		CopyCommonDataArea("SAVED_CONFIG", AREA_CONFIG);
		SaveCommonDataAreaA2("SAVED_CONFIG", SAVE_COMMON_CONFIG);
    }
}