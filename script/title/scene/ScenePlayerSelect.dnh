//Difficulty Selection Screen
function<int> ScenePlayerSelect(int selectedDefault_) {
    bool exitFlag = false;
    int playMode = GetAreaCommonData(AREA_TITLE, "GameMode", GAME_NONE);
    int selectedDifficulty = GetAreaCommonData(AREA_TITLE, "Difficulty", D_EASY);
    
    //Player Icons
    string[] imgPlayerPortrait = [IMG_TT_PLAYER1, IMG_TT_PLAYER2];

    int selectMax = length(imgPlayerPortrait) - 1;
    int resultIndex = -1;

    int indexCurrent = selectedDefault_;

    int[] forbidIndex = [];
    
    // Check if player has unlocked this difficulty (defaults to true)
    bool[] unlockFlag = [true, true];

    //Extra Unlock
    if (selectedDifficulty >= D_EASYEX) {
        unlockFlag = [
            CD_GetExtraStageFlag(PLAYER_YABUSAME), 
            CD_GetExtraStageFlag(PLAYER_TSUBAKURA)
            ];
    }
    //additional else if branch for practice mode unlocks

    //Forbid Indices
    ascent (i in 0..length(unlockFlag)) {
        if (!unlockFlag[i]) forbidIndex = forbidIndex ~ [i];
    }

    //Don't select forbidden index as default
    while (CheckForbidden(indexCurrent, forbidIndex)) {
        indexCurrent = IncrementIndex(indexCurrent, 1, 0, selectMax, forbidIndex);
    }

    TCreateMenuGraphicsPlayer();

    //Create Menu Items
    int menuX = WINDOW_WIDTH / 2 - 128;
    int menuY = WINDOW_HEIGHT / 2;
    ascent (i in 0..selectMax + 1) {
        TCreateMenuItemPlayer(i, menuX, menuY);
        menuX = WINDOW_WIDTH / 2 + 128;
    }

    wait(15); //wait for screen fade from previous menu to clear
    WaitKeysFree([VK_LEFT, VK_RIGHT, VK_OK, VK_CANCEL, VK_PAUSE]);
    loop {
        int userKey = CheckKeyPressed([VK_LEFT, VK_RIGHT, VK_OK, VK_CANCEL, VK_PAUSE]);
        alternative (userKey)
        case (VK_LEFT) {
            CallSFX(SND_TT_SELECT);
            indexCurrent = IncrementIndex(indexCurrent, -1, 0, selectMax, forbidIndex);
        }
        case (VK_RIGHT) {
            CallSFX(SND_TT_SELECT);
            indexCurrent = IncrementIndex(indexCurrent, 1, 0, selectMax, forbidIndex);
        }
        case (VK_OK) {
            resultIndex = indexCurrent;
            MenuSelect();
            break;
        }
        case (VK_CANCEL, VK_PAUSE) {
            CallSFX(SND_TT_CANCEL);
            ScreenBlackout(10, 5, 10, COLOR_BLACK);
            wait(10);
            break;
        }
    }
    exitFlag = true;
    return(resultIndex);

    function<void> MenuSelect() {
        CallSFX(SND_TT_DECIDE);
        //white screen flash then fadeout
        ScreenBlackout(0, 0, 20, COLOR_WHITE);
        wait(60);
        ScreenBlackout(60, 60, 0, COLOR_BLACK);
        wait(60);
    }

    task TCreateMenuGraphicsPlayer() {
        int bg = InitSprite2D(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, RP_UI_BG);
        ObjRender_SetColor(bg, COLOR_BLACK);
        int circleOut = InitSprite2D(486, 0, 1017, 400, IMG_TT_PLAYERBG, RP_UI_BG);
        ObjRender_SetPosition(circleOut, 55, 0, 1);
        int circleIn = InitSprite2D(0, 0, 486, 400, IMG_TT_PLAYERBG, RP_UI_IMG);
        ObjRender_SetPosition(circleIn, 78, 0, 1);

        int radius = 241;
        TCircles();
        while (!exitFlag) {
            float angleT = rand(-50, 50); //get angle from center to edge of circle that is on screen
            MoveParticle(WINDOW_WIDTH_MID + radius * cos(angleT), WINDOW_HEIGHT_MID + radius * sin(angleT), angleT);
            MoveParticle(WINDOW_WIDTH_MID + radius * cos(angleT + 180), WINDOW_HEIGHT_MID + radius * sin(angleT + 180), angleT + 180);
            yield;
        }
        Obj_Delete(bg);
        Obj_Delete(circleOut);
        Obj_Delete(circleIn);

        task TCircles() {
            int primCircle = InitPrim2D(PRIMITIVE_LINESTRIP, 33, RP_UI_BG);
            int vertices = ObjPrim_GetVertexCount(primCircle);

            float tempRadius = radius;
            int[] color = [COLOR_WHITE, COLOR_GRAY_L, COLOR_GRAY, COLOR_GRAY_D];
            int frame = 0;
            float frames = 120; //to avoid int division
            while (!exitFlag) {
                float angleT = 0;
                tempRadius = Interpolate_Decelerate(radius, 0, frame / frames);
                for (int i = 0; i < ObjPrim_GetVertexCount(primCircle); i++) {
                    ObjPrim_SetVertexPosition(primCircle, i, WINDOW_WIDTH_MID + tempRadius * cos(angleT), WINDOW_HEIGHT_MID + tempRadius * sin(angleT), 1);
                    ObjPrim_SetVertexColor(primCircle, i, color[frame * 4 ~/ frames]);
                    angleT += 360 / (vertices - 1);
                }
                frame++;
                if (frame >= frames) frame = 0;
                yield;
            }
            Obj_Delete(primCircle);
        }

        task MoveParticle(float x_, float y_, float angle_) {
            int[] color = [COLOR_WHITE, COLOR_GRAY_L, COLOR_GRAY, COLOR_GRAY_D];
            float maxSpeed = rand(2, 3.4);
            float speed = maxSpeed;
            int frame = 0;
            int frameRate = 20;
            while (!exitFlag) {
                //add particle
                ObjParticleList_SetPosition(_titleList, x_, y_, 1);
                ObjParticleList_SetExtraData(_titleList, trunc(3 - (frame / frameRate)), 0, 0);
                ObjParticleList_SetColor(_titleList, color[frame / frameRate]);
                ObjParticleList_AddInstance(_titleList);
                //update position variables
                x_ += speed * cos(angle_);
                y_ += speed * sin(angle_);
                speed = Interpolate_Decelerate(maxSpeed, 0, frame / (frameRate * 4.0));
                frame++;
                if (frame >= frameRate * 4) break;
                else yield;
            }
        }
    }

    task TCreateMenuItemPlayer(int index_, int x_, int y_) {
        int objPlayerShadow = InitSprite2D(0, 160, 224, 320, imgPlayerPortrait[index_], RP_UI_IMG);
        ObjSprite2D_SetDestCenter(objPlayerShadow);
        ObjRender_SetPosition(objPlayerShadow, x_, y_, 1);

        int objPlayerBack = InitSprite2D(224, 0, 448, 160, imgPlayerPortrait[index_], RP_UI_IMG);
        ObjSprite2D_SetDestCenter(objPlayerBack);
        ObjRender_SetPosition(objPlayerBack, x_, y_, 1);

        int objPlayerFront = InitSprite2D(0, 0, 224, 160, imgPlayerPortrait[index_], RP_UI_IMG);
        ObjSprite2D_SetDestCenter(objPlayerFront);
        ObjRender_SetPosition(objPlayerFront, x_ - 8, y_ - 8, 1);

        while (!exitFlag) {
            Obj_SetVisible(objPlayerFront, index_ == indexCurrent);
            Obj_SetVisible(objPlayerBack, index_ != indexCurrent);
            yield;
            if (index_ == resultIndex) ObjSprite2D_SetSourceRect(objPlayerFront, 224, 160, 448, 320);
        }
        Obj_Delete(objPlayerFront);
        Obj_Delete(objPlayerBack);
        Obj_Delete(objPlayerShadow);
    }
}

