//Difficulty Selection Screen
function<int> SceneRankSelect(int selectedDefault_) {
    bool exitFlag = false; //Delete certain things upon leaving this menu
    int playMode = GetAreaCommonData(AREA_TITLE, "PlayMode", GAME_NONE);

    bool extraMode = (selectedDefault_ == D_EXTRA || selectedDefault_ == D_ABEX);
    int[] imgValue = [0, 1, 2, 3];

    //Extra Stage menu
    if (extraMode) {
        selectedDefault_ = 0;
        imgValue = [4, 5];
    }

    int selectMax = length(imgValue) - 1;
    int resultIndex = -1; //value returned from this menu

    int indexCurrent = selectedDefault_;

    int[] forbidIndex = [];

    TCreateMenuGraphicsRank();

    //Create Menu Items
    int menuX = GetScreenWidth() / 2;
    int menuY = GetScreenHeight() / 2 - (25 * selectMax);
    ascent (i in 0..selectMax + 1) {
        TCreateMenuItemRank(i, menuX, menuY, imgValue[i]);
        menuY += 50;
    }

    wait(15); //wait for screen fade from previous menu to clear
    WaitKeysFree([VK_UP, VK_DOWN, VK_OK, VK_CANCEL, VK_PAUSE]);
    loop {
        int userKey = CheckKeyPressed([VK_UP, VK_DOWN, VK_OK, VK_CANCEL, VK_PAUSE]);
        alternative (userKey)
        case (VK_UP) {
            CallSFX(SND_TT_SELECT);
            indexCurrent = IncrementIndex(indexCurrent, -1, 0, selectMax, forbidIndex);
        }
        case (VK_DOWN) {
            CallSFX(SND_TT_SELECT);
            indexCurrent = IncrementIndex(indexCurrent, 1, 0, selectMax, forbidIndex);
        }
        case (VK_OK) {
            resultIndex = indexCurrent;
            MenuSelect();
            break;
        }
        case (VK_CANCEL, VK_PAUSE) {
            CallSFX(SND_TT_CANCEL);
            ScreenBlackout(10, 5, 10, COLOR_BLACK);
            wait(10);
            break;
        }
    }
    exitFlag = true;
    if (resultIndex == 0 && extraMode) resultIndex = D_EXTRA;
    if (resultIndex == 1 && extraMode) resultIndex = D_ABEX;
    SetAreaCommonData(AREA_TITLE, "Difficulty", resultIndex);
    return(resultIndex);

    function<void> MenuSelect() {
        CallSFX(SND_TT_DECIDE);
        ScreenBlackout(10, 5, 10, COLOR_BLACK);
        wait(10);
    }

    task TCreateMenuGraphicsRank() {
        int bg = InitSprite2D(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, IMG_TT_BG, RP_UI_BG);

        while (!exitFlag) yield;
        Obj_Delete(bg);
    }

    task TCreateMenuItemRank(int index_, int x_, int y_, int imgIndex_) {
        int objText = InitSprite2D(0, imgIndex_ * 16, 223, (imgIndex_ + 1) * 16, IMG_TT_RANK, RP_UI_IMG);
        ObjSprite2D_SetDestCenter(objText);
        ObjRender_SetPosition(objText, x_, y_, 1);

        while (!exitFlag) {
            ObjRender_SetColor(objText, index_ == indexCurrent ? COLOR_YELLOW : COLOR_WHITE);
            yield;
        }
        Obj_Delete(objText);
    }
}