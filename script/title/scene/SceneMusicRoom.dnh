//Music Room
#include "../lib/BGMData.dnh"

function SceneMusicRoom() {
    let exitFlag = false;

    let selectMax = 19; //number of tracks - 1 (starts from index 0)
    let indexListSize = 7; //number of tracks on screen at once - 1 (starts from index 0)
    let indexListTop = 0; //index at the top of the track list, used for scrolling the list

    let indexCurrent = 0;

    let revealPrompt = false; //has the player attempted to play an undiscovered track?
    let revealIndex = -1; //used to reveal unknown tracks on prompt

    CreateMenuGraphicsMusic();
    //default texts
    let playingText = CreateText(32, 268, GetBGMText(-1)[0], 0xFFFFFFFF); //currently playing track
    let descriptionText = CreateText(32, 288, GetBGMText(-1)[1], 0xFFFFFFFF); //track comments

    //Create Menu Items
    local {
        let menuX = 32;
        let menuY = 64;
        ascent(i in 0..indexListSize + 1) {
            CreateMenuItemMusic(i, menuX, menuY);
            menuY+= 16;
        }
    }
    ClearBGM(); //fade out title music
    wait(15); //wait for screen fade from previous menu to clear
    WaitKeysFree([VK_UP, VK_DOWN, VK_OK, VK_CANCEL, VK_PAUSE]);
    loop {
        let userKey = CheckKeyPressed([VK_UP, VK_DOWN, VK_OK, VK_CANCEL, VK_PAUSE]);
        alternative (userKey)
        case (VK_UP) {
            //CallSFX(SND_TT_SELECT);
            indexCurrent = IncrementIndex(indexCurrent, -1, 0, selectMax);
            if (revealPrompt == true) {
                CancelReveal();
            }
        }
        case (VK_DOWN) {
            //CallSFX(SND_TT_SELECT);
            indexCurrent = IncrementIndex(indexCurrent, 1, 0, selectMax);
            if (revealPrompt == true) {
                CancelReveal();
            }
        }
        case (VK_OK) {
            MenuSelect();
        }
        case (VK_CANCEL, VK_PAUSE) {
            CallSFX(SND_TT_Cancel);
            ScreenBlackout(10, 5, 10, 0x000000);
            ClearBGM();
            wait(10);
            break;
        }
        //check if cursor has scrolled outside music list
        if (indexCurrent > indexListTop + indexListSize) {
            indexListTop = indexCurrent - indexListSize;
        } else if (indexCurrent < indexListTop) {
            indexListTop = indexCurrent;
        }
    }
    exitFlag = true;
    Obj_Delete(descriptionText);
    Obj_Delete(playingText);

    function MenuSelect() {
        //play music track if heard previously, change description text
        if (!CD_GetBGMHeardFlag(indexCurrent)) { //prompt player on selecting undiscovered music
            if (revealPrompt) {
                revealIndex = indexCurrent;
                revealPrompt = false;
            } else {
                ObjText_SetVertexColor(playingText, 0xFFFF0000);
                ObjText_SetVertexColor(descriptionText, 0xFFFF0000);
                ObjText_SetText(playingText, "???");
                ObjText_SetText(descriptionText, "are you sure?");
                revealPrompt = true;
                return;
            }
        }
        ClearBGM(); //allows restarting the same track if it's already playing
        yield;
        StartBGM(indexCurrent, 15, 15);
        ObjText_SetVertexColor(playingText, 0xFFFFFFFF);
        ObjText_SetVertexColor(descriptionText, 0xFFFFFFFF);
        ObjText_SetText(playingText, GetBGMText(indexCurrent)[0]);
        ObjText_SetText(descriptionText, GetBGMText(indexCurrent)[1]);
    }

    task CreateMenuGraphicsMusic() {
        let bg = ObjPrim_Create(OBJ_SPRITE_2D);
        ObjPrim_SetTexture(bg, IMG_TT_BG);
        Obj_SetRenderPriorityI(bg, 1);
        ObjSprite2D_SetSourceRect(bg, 0, 0, 640, 400);
        ObjSprite2D_SetDestRect(bg, 0, 0, 640, 400);

        while (!exitFlag) {yield;}
        Obj_Delete(bg);
    }

    task CreateMenuItemMusic(index, x, y) {
        let objText;
        let objTextHighlight;
        let bgmID = indexListTop + index; //actual track number = index at top of list + position in the list
        let indexNumber = vtos("2d", bgmID+1) ~ ". "; //prepended track number

        //Initial list creation
        if (!CD_GetBGMHeardFlag(bgmID)) {
            objText = CreateText(x, y, indexNumber ~ "???", 0xFFFFFFFF);
            objTextHighlight = CreateText(x, y, indexNumber ~ "???", 0xFFFFFF00);
        } else {
            objText = CreateText(x, y, indexNumber ~ GetBGMText(bgmID)[0], 0xFFFFFFFF);
            objTextHighlight = CreateText(x, y, indexNumber ~ GetBGMText(bgmID)[0], 0xFFFFFF00);
        }

        while (!exitFlag) {
            if (indexListTop + index != bgmID) { //update list if it scrolls beyond the current boundaries
                bgmID = indexListTop + index;
                indexNumber = vtos("2d", bgmID+1) ~ ". ";
                if (!CD_GetBGMHeardFlag(bgmID)) {
                    ObjText_SetText(objText, indexNumber ~ "???");
                    ObjText_SetText(objTextHighlight, indexNumber ~ "???");
                } else {
                    ObjText_SetText(objText, indexNumber ~ GetBGMText(bgmID)[0]);
                    ObjText_SetText(objTextHighlight, indexNumber ~ GetBGMText(bgmID)[0]);
                }
            }
            if (revealIndex == indexListTop + index) { //play undiscovered track
                ObjText_SetText(objText, indexNumber ~ GetBGMText(bgmID)[0]);
                ObjText_SetText(objTextHighlight, indexNumber ~ GetBGMText(bgmID)[0]);
                revealIndex = -1;
            }
            Obj_SetVisible(objTextHighlight, bgmID == indexCurrent); //highlight selected index
            yield;
        }
        Obj_Delete(objText);
        Obj_Delete(objTextHighlight);
    }

    function CancelReveal() { //resets currently playing track and desription text upon cancelling a track reveal
        ObjText_SetVertexColor(playingText, 0xFFFFFFFF);
        ObjText_SetVertexColor(descriptionText, 0xFFFFFFFF);
        ObjText_SetText(playingText, GetBGMText(GetAreaCommonData(AREA_TITLE, "BGM_ID", -1))[0]);
        ObjText_SetText(descriptionText, GetBGMText(GetAreaCommonData(AREA_TITLE, "BGM_ID", -1))[1]);
        revealPrompt = false;
    }
}