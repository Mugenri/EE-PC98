//Stage Control Script
#include "./SceneResult.dnh"
#include "./SceneReplay.dnh"
#include "./../lib/lib_TitleCommon.dnh"
#include "./../lib/lib_TitleUtil.dnh"
#include "./../../lib/lib_HighScore.dnh"
#include "./../../lib/lib_Load.dnh"
#include "./../../lib/lib_Result.dnh"

int _stageEvent;
bool _pauseEnable;
string _dialoguePath;

@Initialize {
    SetAreaCommonData(AREA_TITLE, "SCRIPT_STAGE", GetOwnScriptID());
    SetAutoDeleteObject(true);
    TStageScene();
    ClearBGM();
}

@Event {
    alternative (GetEventType())
    case (EV_PAUSE_ENABLE) {
        _pauseEnable = GetEventArgument(0);
    }
    case (EV_STAGE_EVENT) {
        int event = GetEventArgument(0);
        alternative (event)
        case (STAGE_DIALOGUE) {
            _stageEvent = event;
            _dialoguePath = GetEventArgument(1);
        }
        others { _stageEvent = event; }
    }
}

@MainLoop { yield; }

task TStageScene() {
    int gameMode        = GetGameMode();
    int playerType      = GetPlayerType();
    string pathPlayer   = SCRIPT_PLAYERS[playerType];
    int stageIndex      = GetStageIndex();
    string replayPath   = GetAreaCommonData(AREA_TITLE, "ReplayPath", "");
    bool saveReplay     = false; //toggle to true by replay save option on menus

    _stageEvent = STAGE_NONE;
    _pauseEnable = false;
    _dialoguePath = "";

    //reset common data areas before running a new stage
    ClearAreaCommonData(AREA_STAGE);
    ClearAreaCommonData(AREA_SYSTEM);

    //load textures
    Load_LoadTextureStage(stageIndex);
    Load_LoadTexturePlayer(playerType);
    if (gameMode == GAME_MAIN) Load_LoadTextureDialogue(stageIndex, playerType);

    InitializeStageScene();

    if (length(replayPath) > 0) {
        SetStageReplayFile(replayPath);
        SetSkipModeKey(KEY_LCONTROL);
    }

    string stageScript;
    alternative (gameMode)
    case (GAME_MAIN) { stageScript = GetStagePath(stageIndex); }
    case (GAME_SPELL) { stageScript = DIR_STG ~ "Stage_SpellPractice.dnh"; }

    wait(20);

    StartStage();

    if (gameMode == GAME_MAIN && !IsReplay()) {
        Result_AddPlayCount(GetPlayerType(), GetDifficulty());
    }

    loop {
        //--------------------------------------------------------------------
        //Stage Event Handling
        //--------------------------------------------------------------------
        //Player pause
        if (GetVirtualKeyState(VK_PAUSE) == KEY_PUSH && _pauseEnable && _stageEvent == STAGE_NONE) {
            if (!IsReplay()) {
                alternative (gameMode)
                case (GAME_MAIN) _stageEvent = STAGE_PAUSE;
                case (GAME_SPELL) _stageEvent = STAGE_PAUSE_SPELL;
            } else _stageEvent = STAGE_PAUSE_REPLAY;
        }
        //stage event (any event that results in termination shouldn't reset _stageEvent)
        if (_stageEvent != STAGE_NONE) {
            alternative (_stageEvent)
            case (STAGE_PAUSE, STAGE_PAUSE_SPELL, STAGE_PAUSE_REPLAY, STAGE_GAMEOVER) { //pause and continue menus
                if (IsReplay() && _stageEvent == STAGE_GAMEOVER) _stageEvent = STAGE_GAMEOVER_REPLAY; //skip continue prompt during replay
                int menuResult = StageMenu(_stageEvent);
                HandleMenuResult(menuResult, false);
                if (menuResult == RESULT_RETRY) return;
                if (IsReplay() && menuResult == RESULT_END) break;
            }
            case (STAGE_DIALOGUE) {
                RunDialogue(_dialoguePath);
                _stageEvent = STAGE_NONE;
            }
            case (STAGE_VICTORY) {
                EndStage();
            }
            case (STAGE_VICTORY_SPELL) {
                SetTransitionTexture();
                StageCleared();
            }
            case (STAGE_GAMEOVER_SPELL) {
                SetTransitionTexture();
                TerminateStageScene();
            }
        }
        //--------------------------------------------------------------------
        //Stage Result
        //--------------------------------------------------------------------
        int stageState = GetStageSceneState();
        if (stageState == STAGE_STATE_FINISHED) {
            if (IsReplay()) {
                int menuResult = StageMenu(STAGE_GAMEOVER_REPLAY);
                HandleMenuResult(menuResult, true);
                if (menuResult == RESULT_RETRY) return;
                break;
            }

            FinalizeStageScene();

            int stageResult = GetStageSceneResult();
            alternative (stageResult)
            case (STAGE_RESULT_CLEARED) { //Game Won
                WriteLog("won");
                alternative (_stageEvent)
                case (STAGE_VICTORY) { //game clear
                    HandleStageVictory();
                    //TODO endings/credits maybe clearbgm and start ending song idk
                    SceneResult(GetScore(), "Cleared");
                    saveReplay = SaveReplayPrompt(GetAreaCommonData(AREA_STAGE, "Continues", 0));
                    WriteLog("stage victory");
                }
                case (STAGE_VICTORY_SPELL) { //spell practice clear
                    WriteLog("stage victory spell");
                    int menuResult = StageMenu(_stageEvent);
                    HandleMenuResult(menuResult, true);
                    if (menuResult == RESULT_RETRY) return;
                }
                break;
            }
            case (STAGE_RESULT_BREAK_OFF) { //Exit Game
                WriteLog("break off");
                alternative (_stageEvent)
                case (STAGE_GAMEOVER) { //quit from continue prompt
                    //bad ending here?
                    SceneResult(GetScore(), "Failed");
                    saveReplay = SaveReplayPrompt(GetAreaCommonData(AREA_STAGE, "Continues", 0));
                    WriteLog("stage gameover");
                }
                case (STAGE_GAMEOVER_SPELL) { //spell practice fail
                    WriteLog("stage gameover spell");
                    int menuResult = StageMenu(_stageEvent);
                    HandleMenuResult(menuResult, true);
                    if (menuResult == RESULT_RETRY) return;
                }
                break;
            }
        }
        yield;
    }

    if (IsReplay() && !DEBUG_MODE) SetSkipModeKey(KEY_INVALID);
    if (saveReplay) SceneReplay(0, true);
    TReturnToTitle();

    function<void> StartStage() {
        SetStageIndex(stageIndex);
        SetStageMainScript(stageScript);
        SetStagePlayerScript(pathPlayer);
        StartStageScene();
    }

    function<int> StageMenu(_menuType) {
        if (_menuType == STAGE_PAUSE) StopBGM(); //pause music
        SetTransitionTexture(); //save current frame as pause bg
        PauseStageScene(true); //pause stage
        
        string pathScript = GetModuleDirectory() ~ "script/system/StageMenu.dnh";
        
        //Run pause script
        int idScript = LoadScript(pathScript);
        SetScriptArgument(idScript, 0, _menuType);
        StartScript(idScript);
        
        while (!IsCloseScript(idScript)) yield;

        return GetScriptResult(idScript);
    }

    function<void> HandleMenuResult(int menuResult_, bool isStageEnd_) {
        alternative (menuResult_)
        case (RESULT_CANCEL) {
            ResumeBGM();
            PauseStageScene(false);
            _stageEvent = STAGE_NONE;
        }
        case (RESULT_RETRY) {
            if (gameMode == GAME_MAIN) ClearBGM();
            if (_stageEvent == STAGE_GAMEOVER) Result_AddNew(GetScore(), GetCurrentDateTimeS(), "Failed"); //retry from continue menu
            UpdateHighScore();
            if (!isStageEnd_) {
                TerminateStageScene();
                yield;
                FinalizeStageScene();
            }
            TStageScene();
        }
        case (RESULT_CONTINUE) {
            UpdateHighScore();
            //save continue score to result menu
            Result_AddNew(GetScore(), GetCurrentDateTimeS(), "Continue");
            //use continue
            NotifyEvent(GetAreaCommonData(AREA_STAGE, "SCRIPT_STAGE", 0), EV_CONTINUE, 0);
            PauseStageScene(false);
            _stageEvent = STAGE_NONE;
        }
        case (RESULT_SAVE_REPLAY) {
            saveReplay = true;
            wait(30);
        }
        case (RESULT_END) {
            if (!isStageEnd_) TerminateStageScene();
            wait(60);
        }
    }

    function<void> RunDialogue(string path_) {
        if (IsReplay()) return;
        SetTransitionTexture();
        PauseStageScene(true);

        //Start dialogue
        int idScript = LoadScript(path_);
        StartScript(idScript);

        while (!IsCloseScript(idScript)) yield;

        //resume
        PauseStageScene(false);
        return;
    }

    function<void> EndStage() { //fade to black, dim music
        SetTransitionTexture();
        RenderToTextureA1(RT_ENDSCREEN, RP_STG_END, RP_STG_END, true);
        StageCleared();
        ClearBGM();

        int bg = InitSprite2D(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, RT_SCREENSHOT, RP_UI_BG);
        int objBlack = InitSprite2D(0, 0, MAX_X, MAX_Y, RP_UI_IMG);
        int objText = InitSprite2D(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, RT_ENDSCREEN, RP_UI_IMG);
        ObjRender_SetColor(objBlack, COLOR_BLACK);
        ObjRender_SetPosition(objBlack, 32, 16, 1); //offset to cover game window

        int fadeTime = 180;
        for (int i = 1; i <= fadeTime; i++) {
            ObjRender_SetAlpha(objBlack, Interpolate_Linear(0, 255, i / as_float(fadeTime)));
            yield;
        }

        SetTransitionTexture();

        wait(30);
        Obj_Delete(bg);
        Obj_Delete(objBlack);
        Obj_Delete(objText);
        if (!IsReplay()) wait(60);
    }

    function<void> HandleStageVictory() {
        int diff = GetDifficulty();
        Result_AddClearCount(playerType, diff);

        CD_SetGameClearFlag(playerType, diff);

        //unlock character
        if (playerType == PLAYER_YABUSAME) {
            CD_SetExtraStageFlag(PLAYER_TSUBAKURA);
        } else if (playerType == PLAYER_TSUBAKURA) {
            //tsurubami?
        }
    }
}

function<void> StageCleared() { //close stage, result is STAGE_RESULT_CLEARED
    NotifyEvent(GetStageScriptID(), EV_STAGE_VICTORY, 0);
}

function<void> SetTransitionTexture() { //saves the current frame to be used as the background for pause/dialogue screens
	RenderToTextureA1(RT_SCREENSHOT, RP_UI_MIN, RP_STG_BG, false);
    RenderToTextureA1(RT_SCREENSHOT, RP_STG_TRANSITION2, RP_UI_MAX, false);
}

function<void> TReturnToTitle() { //Run title screen
    int gameMode = GetGameMode(); //before clearing common data, get game mode
    
    if (!IsReplay()) {
        UpdateHighScore();
        //add playtime
        if (gameMode == GAME_MAIN) Result_AddPlayTime(GetPlayerType(), GetDifficulty(), GetStageTime());
        //save data
        CD_SaveScore();
        CD_SaveResult();
    }

    //reset relevant common data
    ClearAreaCommonData(AREA_STAGE);
    ClearAreaCommonData(AREA_SYSTEM);

    //uncache scripts/textures
    {
        int stageIndex = GetStageIndex();
        int playerType = GetPlayerType();
        Load_UncacheScripts(stageIndex, playerType, gameMode);
        Load_UnloadTextureStage(stageIndex);
        Load_UnloadTexturePlayer(playerType);
        if (gameMode == GAME_MAIN) Load_UnloadTextureDialogue(stageIndex, playerType);
    }

    //reset replay path
    SetAreaCommonData(AREA_TITLE, "ReplayPath", "");

    //run title screen
    string path = GetModuleDirectory() ~ "script/title/Title.dnh";
    int ID = LoadScript(path);
    int preloadIndex = IsReplay() ? 2 : gameMode == GAME_SPELL ? 1 : -1;
    SetScriptArgument(ID, 0, preloadIndex);
    StartScript(ID);
    UnloadScriptFromCache(path);

    CloseScript(GetOwnScriptID());
}

//return stage script using stage index
function<string> GetStagePath(int index_) {
    alternative (index_)
    case (0) return DIR_STG ~ "tsurubami/tsuruStage.dnh"; //yabusame stage
    case (1) return DIR_STG ~ "tsurubami/tsuruStage.dnh"; //tsubakura stage
    others assert(false, index_ ~ " is an invalid stage index.");
}

//update high score with conditions
function<void> UpdateHighScore() {
    if (GetAreaCommonData(AREA_STAGE, "Continues", 0) == 0 && !IsReplay()) {
        HS_SetHighScore(GetScore(), HS_GetDefaultScore(GetGameMode()));
    }
}