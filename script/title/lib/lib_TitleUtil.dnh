//Title Screen utility library
#include "./../../lib/lib_Util.dnh"

//Create Menu Cursor
function<int> CreateCursor(int x_, int y_) {
    int obj = InitSprite2D(0, 0, 128, 16, IMG_TT_CURSOR, RP_UI_TEXT);
    ObjSprite2D_SetDestCenter(obj);
    ObjRender_SetPosition(obj, x_, y_, 1);
    return obj;
}

//Screen fade animation
function<int> ScreenBlackout(int fadeOut_, int between_, int fadeIn_, int color_) {
    int obj = InitSprite2D(0, 0, WINDOW_WIDTH, WINDOW_HEIGHT, RP_UI_MAX);
    ObjRender_SetColor(obj, color_);

    async {
        for (real i = 1; i <= fadeOut_; i++) {
            if (Obj_IsDeleted(obj)) break;
            ObjRender_SetAlpha(obj, Interpolate_Linear(0, 255, i / fadeOut_));
            yield;
        }
        ObjRender_SetAlpha(obj, 255);
        loop (between_) {
            if (Obj_IsDeleted(obj)) break;
            yield;
        }
        for (real i = 1; i <= fadeIn_; i++) {
            if (Obj_IsDeleted(obj)) break;
            ObjRender_SetAlpha(obj, Interpolate_Linear(255, 0, i / fadeIn_));
            yield;
        }
        if (!Obj_IsDeleted(obj)) Obj_Delete(obj);
    }

    return obj;
}

