//Item Script, also handles shot deletion
#include "./lib/lib_SystemCommon.dnh"
#include "./lib/lib_AsciiNum.dnh"
#include "./../lib/effect/lib_EffectCancel.dnh"

@Initialize {
    SystemInit();
    SetAreaCommonData(AREA_SYSTEM, "SCRIPT_ITEM", GetOwnScriptID());
    SetItemAutoDeleteClip(32, 256, 32, 64);
    SetDefaultBonusItemEnable(false);
    LoadItemData(GetCurrentScriptDirectory() ~ "/lib/dat_ItemData.dnh");
    InitEffectCancel();
    InitAsciiList();
}

@MainLoop {yield;}

@Event {
    alternative (GetEventType())
    case (EV_DELETE_SHOT_IMMEDIATE) { //triggered by player bomb/death
        int obj = GetEventArgument(0);
        real[] pos = GetEventArgument(1);
        int score = Obj_GetValueDI(obj, SHOT_SCORE, 100); //TODO this should probably be assumed 0

        AddScore(score);
        if (Obj_GetType(obj) == OBJ_SHOT) DeleteEffectShot(obj);
        else if (Obj_GetType(obj) == OBJ_STRAIGHT_LASER) DeleteEffectLaser(obj);
        WriteLog("delete");
    }
    case (EV_DELETE_SHOT_TO_ITEM) { //triggered by clearing a boss spell, creates score text
        int obj = GetEventArgument(0);
        real[] pos = GetEventArgument(1);
        int score = Obj_GetValueDI(obj, SHOT_SCORE, 12800); //assume all shots without an assigned score are max value

        //create item in place of bullet
        if (Obj_GetValueDI(obj, SHOT_DROP_ITEM, false)) NotifyEvent(GetOwnScriptID(), EV_CREATE_ITEM, pos[0], pos[1], ITEM_SCORE);
        AddScore(score);
        if (Obj_GetType(obj) == OBJ_SHOT) {
            _objScene = GetEnemyBossSceneObjectID();
            if (ObjEnemyBossScene_GetInfo(_objScene, INFO_IS_SPELL)) AsciiScoreNum(pos[0], pos[1], score);
            DeleteEffectShot(obj);
        } else if (Obj_GetType(obj) == OBJ_STRAIGHT_LASER) DeleteEffectLaser(obj);
    }
    case (EV_CREATE_ITEM) {
        int obj = CreateItemU1(GetEventArgument(2), GetEventArgument(0), GetEventArgument(1), 0);
        ObjMove_AddPatternB2(obj, 0, 0, -3.2, 0, 0.06, 0, 4);
        ObjItem_SetAutoCollectEnable(obj, false);
        ObjItem_SetRenderScoreEnable(obj, false);
    }
    case (EV_GET_ITEM) {
        int type = GetEventArgument(0);
        int obj = GetEventArgument(1);
        int score = 10;
        int color = COLOR_WHITE;

        alternative (type)
        case (ITEM_POW) {
            WriteLog("No Power System");
        }
        case (ITEM_SCORE) {
            if (ObjRender_GetY(obj) < 128) {
                score = 51200;
                color = COLOR_YELLOW_L;
            } else {
                score = 25600 - 25600*((ObjRender_GetY(obj) - 128)/GetStgFrameHeight());
                score -= score % 10; //cuts off last digit, rounds down
            }
            //PLAY SFX
        }
        case (ITEM_DREAM) {
            WriteLog("No Dream Bonus System");
            //PLAY SFX
        }
        case (ITEM_POW_L) {
            WriteLog("No Power System");
            //PLAY SFX
        }
        case (ITEM_POW_FULL) {
            WriteLog("No Power System");
            //PLAY SFX
        }
        case (ITEM_BOMB) {
            score = 1000;
            SetPlayerSpell(GetPlayerSpell() + 1);
            //PLAY SFX
            //TODO spell extend text
        }
        case (ITEM_EXTEND) {
            score = 1000;
            SetPlayerLife(GetPlayerLife() + 1);
            //PLAY SFX
            //TODO extend text
        }
        AddScore(score);
        AsciiScoreNum(ObjRender_GetX(obj), ObjRender_GetY(obj), score, color);
    }
}