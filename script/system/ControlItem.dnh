//Item Script, also handles shot deletion
#include "./lib/lib_CommonSystem.dnh"
#include "./lib/lib_DeleteEffect.dnh"
#include "./lib/lib_AsciiNum.dnh"

@Initialize {
    SetAreaCommonData(AREA_SYSTEM, "SCRIPT_ITEM", GetOwnScriptID());
    SetItemAutoDeleteClip(32, 64, 32, 64);
    SetDefaultBonusItemEnable(false);
    LoadItemData(GetCurrentScriptDirectory() ~ "/lib/dat_ItemData.dnh");
    InitEffectList();
}

@MainLoop {yield;}

@Event {
    alternative (GetEventType())
    case (EV_DELETE_SHOT_IMMEDIATE) { //triggered by player bomb/death
        int obj = GetEventArgument(0);
        real[] pos = GetEventArgument(1);
        int score = Obj_GetValueDI(obj, BULLET_SCORE, 100);

        AddScore(score);
        if (Obj_GetType(obj) == OBJ_SHOT) {
            DeleteEffectShot(obj);
        }
        WriteLog("delete");
    }
    case (EV_DELETE_SHOT_TO_ITEM) { //triggered by clearing a boss spell, creates score text
        int obj = GetEventArgument(0);
        real[] pos = GetEventArgument(1);
        int score = Obj_GetValueDI(obj, BULLET_SCORE, 100);

        if (Obj_GetValueDI(obj, BULLET_DROP_ITEM, false)) { //create item in place of bullet
            NotifyEvent(GetOwnScriptID(), EV_CREATE_ITEM, pos[0], pos[1], ITEM_SCORE);
        }

        AddScore(score);
        if (Obj_GetType(obj) == OBJ_SHOT) {
            AsciiScoreNum(pos[0], pos[1], score);
            DeleteEffectShot(obj);
        }
    }
    case (EV_CREATE_ITEM) {
        int obj = CreateItemU1(GetEventArgument(2), GetEventArgument(0), GetEventArgument(1), 0);
        ObjMove_AddPatternB2(obj, 0, 0, -4, 0, 0.08, 0, 4);
        ObjItem_SetAutoCollectEnable(obj, false);
        ObjItem_SetRenderScoreEnable(obj, false);
    }
    case (EV_GET_ITEM) {
        int type = GetEventArgument(0);
        int obj = GetEventArgument(1);
        int score = 10;
        int color = 0xFFFFFF;

        alternative (type)
        case (ITEM_POW) {
            WriteLog("No Power System");
        }
        case (ITEM_SCORE) {
            if (ObjRender_GetY(obj) < 128) {
                score = 51200;
                color = 0xFFFF00;
            } else {
                score = 25600 - 25600*((ObjRender_GetY(obj) - 128)/GetStgFrameHeight());
                score -= score % 10; //cuts off last digit, rounds down
            }
            //PLAY SFX
        }
        case (ITEM_DREAM) {
            WriteLog("No Dream Bonus System");
            //PLAY SFX
        }
        case (ITEM_POW_L) {
            WriteLog("No Power System");
            //PLAY SFX
        }
        case (ITEM_POW_FULL) {
            WriteLog("No Power System");
            //PLAY SFX
        }
        case (ITEM_BOMB) {
            int score = 1000;
            SetPlayerSpell(GetPlayerSpell() + 1);
            //PLAY SFX
        }
        case (ITEM_EXTEND) {
            int score = 1000;
            SetPlayerLife(GetPlayerLife() + 1);
            //PLAY SFX
        }
        AddScore(score);
        AsciiScoreNum(ObjRender_GetX(obj), ObjRender_GetY(obj), score, color);
    }
}