//System Script
#include "./lib/lib_Common.dnh"
#include "./lib/lib_Display.dnh"

@Initialize {
	InitFrame();
    InitCommonData();
    SetAreaCommonData(AREA_SYSTEM, "SCRIPT_SYSTEM", GetOwnScriptID());
    SetItemRenderPriorityI(RP_STG_ITEM);
	SetShotRenderPriorityI(RP_STG_SHOT);

    SetPauseScriptPath(GetCurrentScriptDirectory() ~ "Pause.dnh");
    SetReplaySaveSceneScriptPath(GetCurrentScriptDirectory() ~ "ReplaySaveScene.dnh");
    SetEndSceneScriptPath(GetCurrentScriptDirectory() ~ "EndScene.dnh");
    StartItemScript(GetCurrentScriptDirectory() ~ "ControlItem.dnh");
}

@Event {
	alternative (GetEventType())
	case (EV_START_BOSS_SPELL) {
		//TODO magic circle
	}
	case (EV_GAIN_SPELL) {
		_objScene = GetEnemyBossSceneObjectID();
		int score = ObjEnemyBossScene_GetInfo(_objScene, INFO_SPELL_SCORE);
		TGainSpell(score);
	}
    case (EV_PLAYER_LOADED) { //Requires that the player be loaded.
        TSystemInit();
        TInitDisplay();
    }
}

@MainLoop {
	yield;
}

//----------------------------------------------------
//Sound Event Script
//----------------------------------------------------
function<void> ControlSFX() {
    string path = (GetCurrentScriptDirectory() ~ "./ControlSFX.dnh");
    int id = LoadScript(path);
    StartScript(id);
}

//----------------------------------------------------
//STG Window Frame
//----------------------------------------------------
task TInitDisplay() {
    TIntro();
    Difficulty();
    THighScore();
    TScore();
    TBossLife();
    TBossTimer();
    TPlayerLife();
    TPlayerSpell();
    TGraze();
    if (!IsReplay()) {
        TFPS();
    } else {
        TReplayFPS();
    }
}

//----------------------------------------------------
//Common Data
//----------------------------------------------------
function<void> InitCommonData() {
	if (!IsCommonDataAreaExists(AREA_STAGE)) CreateCommonDataArea(AREA_STAGE);
	if (!IsCommonDataAreaExists(AREA_SYSTEM)) CreateCommonDataArea(AREA_SYSTEM);
	if (!IsCommonDataAreaExists(AREA_REPLAY)) CreateCommonDataArea(AREA_REPLAY);
	LoadSystemCommonData();
}

function<void> LoadSystemCommonData() {
	if (IsReplay()) {
		LoadCommonDataAreaFromReplayFile(AREA_STAGE);
		LoadCommonDataAreaFromReplayFile(AREA_SYSTEM);
	} else {
		SaveCommonDataAreaToReplayFile(AREA_STAGE);
		SaveCommonDataAreaToReplayFile(AREA_SYSTEM);
	}
}