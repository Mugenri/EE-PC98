#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Nonspell 4"]
#System["./../../system/System.dnh"]

#include "./../../lib/lib_Boss.dnh"

int _objBoss;
int _diff = GetDifficulty() - D_EASYEX;
int _count = -60;
int _loopTime = [6, 5, 3][_diff];
_spellID = 18 + _diff;

@Event {
    alternative (GetEventType())
    case (EV_REQUEST_LIFE) {
        SetScriptResult(2600);
    }
    case (EV_REQUEST_TIMER) {
        SetScriptResult(40 + absolute(_count / 60));
    }
}

@Initialize {
    Boss_Init(true);
    _objBoss = Boss_Create(BOSS_ID_TSURUBAMI);
    TInitialize();
    TFinalize();
    TMain();
    TMovement();
}

@MainLoop {
    Boss_Update(_objBoss);
    _count++;
    yield;
}

task TInitialize() {
    ObjMove_SetDestAtFrame(_objBoss, 30, 70, 30, LERP_DECELERATE);
    wait(60);
    Boss_SetVulnerable(_objBoss, true);
}

task TFinalize() {
    while (_bossAlive) yield;
    ObjMove_SetDestAtFrame(_objBoss, MID_X, 60, 60, LERP_DECELERATE);
    Animation_Charge(_objBoss, false);
    Boss_Finalize(_objBoss);
}

task TMain() {
    while (_count < -30) yield;
    while (_bossAlive) {
        Boss_SetIntersection(_objBoss, _intersectionShot, _intersectionPlayer);
        if (_count % _loopTime == 0 && _count >= 0) {
            async {
                TFire();
            }
        }
        yield;
    }
}

task TFire() {
    if (!_bossAlive) return;
    float speed = [3.8, 3.8, 5.2][_diff];
    float modif = [6, 5, 4][_diff];
    int amulet = CreateShotUA1(_bossX, _bossY, speed, _count * modif, SHOT_BLACK+CARD, 0, SHOT_BLACK+SPAWN_S+SIZE_16, SHOT_BLACK+CANCEL_FADE);
    ObjShot_SetSpellResist(amulet, true);
    Laser_WarningLine(_bossX, _bossY, _count * modif, 550, 15);
    CallSFX(SND_SHOT1);

    wait(20);
    modif = [15, 15, 7][_diff];
    int waitTime = [26, 18, 11][_diff];
    float accel = [0.025, 0.025, 0.04][_diff];
    float maxSpeed = [2.0, 2.0, 2.4][_diff];
    while (!Obj_IsDeleted(amulet)) {
        if (!_bossAlive) return;
        float xPos = ObjMove_GetX(amulet);
        float yPos = ObjMove_GetY(amulet);
        float angleT = ObjMove_GetAngle(amulet);
        CreateShotUA2(xPos, yPos, 0, angleT + modif, accel, maxSpeed, 0, SHOT_GRAY_INV+TALISMAN_A, 0, SHOT_GRAY_INV+CANCEL_BURST1);
        CreateShotUA2(xPos, yPos, 0, angleT - modif, accel, maxSpeed, 0, SHOT_GRAY_INV+TALISMAN_A, 0, SHOT_GRAY_INV+CANCEL_BURST1);
        wait(waitTime);
    }
}

task TMovement() {
    while (_count < 0) yield;
    while (_bossAlive) {
        float x = 0.5 + 0.5 * (-cos(_count));
        float y = 0.5 + 0.5 * (sin(_count));
        float xPos = Interpolate_Linear(30, MAX_X - 30, x);
        float yPos = Interpolate_Linear(40, 100, y);
        ObjMove_SetDestAtFrame(_objBoss, xPos, yPos, 1);
        yield;
    }
}