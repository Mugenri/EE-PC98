#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["nonspell4"]
#System["./../../system/System.dnh"]

#include "./../../lib/lib_Boss.dnh"

int _objBoss;
int _diff = GetDifficulty() - 4;
int _count = -60;
int _loopTime = [5, 3][_diff];

@Event {
    alternative (GetEventType())
    case (EV_REQUEST_LIFE) {
        SetScriptResult(2600); 
    }
    case (EV_REQUEST_TIMER) {
        SetScriptResult(40 + absolute(_count / 60));
    }
}

@Initialize {
    Boss_Init(true);
    _objBoss = Boss_Create(BOSS_ID_TSURUBAMI);
    TInitialize();
    TFinalize();
    TMain();
    TMovement();
}

@MainLoop {
    Boss_Update(_objBoss);
    _count++;
    yield;
}

task TInitialize() {
    ObjMove_SetDestAtFrame(_objBoss, 30, 70, 30, LERP_DECELERATE);
    wait(60);
    Boss_SetVulnerable(_objBoss, true);
}

task TFinalize() {
    while (_bossAlive) yield;
    ObjMove_SetDestAtFrame(_objBoss, MID_X, 60, 60, LERP_DECELERATE);
    Animation_Charge(_objBoss, false);
    Boss_Finalize(_objBoss);
}

task TMain() {
    while (_count < -30) yield;
    while (_bossAlive) {
        Boss_SetIntersection(_objBoss, _intersectionShot, _intersectionPlayer);
        if (_count % _loopTime == 0 && _count >= 0) {
            async {
                TFire();
            }
        }
        yield;
    }
}

task TFire() {
    if (!_bossAlive) return;
    real speed = [3.8, 5.2][_diff];
    real modif = [5, 4][_diff];
    int amulet = CreateShotUA1(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), speed, _count * modif, SHOT_BLACK+CARD, 0, SHOT_BLACK+SPAWN_S+SIZE_16, SHOT_BLACK+CANCEL_FADE);
    ObjShot_SetSpellResist(amulet, true);
    Laser_WarningLine(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), _count * modif, 550, 15);
    CallSFX(SND_SHOT1);

    wait(20);
    modif = [15, 7][_diff];
    int waitTime = [18, 11][_diff];
    real accel = [0.025, 0.04][_diff];
    real maxSpeed = [2.0, 2.4][_diff];
    while (!Obj_IsDeleted(amulet)) {
        if (!_bossAlive) return;
        real xPos = ObjMove_GetX(amulet);
        real yPos = ObjMove_GetY(amulet);
        real angleT = ObjMove_GetAngle(amulet);
        CreateShotUA2(xPos, yPos, 0, angleT + modif, accel, maxSpeed, SHOT_GRAY+TALISMAN_A, 0, SHOT_GRAY+CANCEL_BURST1);
        CreateShotUA2(xPos, yPos, 0, angleT - modif, accel, maxSpeed, SHOT_GRAY+TALISMAN_A, 0, SHOT_GRAY+CANCEL_BURST1);
        wait(waitTime);
    }
}

task TMovement() {
    while (_count < 0) yield;
    while (_bossAlive) {
        real x = 0.5 + 0.5 * (-cos(_count));
        real y = 0.5 + 0.5 * (sin(_count));
        real xPos = Interpolate_Linear(30, MAX_X - 30, x);
        real yPos = Interpolate_Linear(40, 100, y);
        ObjMove_SetDestAtFrame(_objBoss, xPos, yPos, 1);
        yield;
    }
}