#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["nonspell4"]
#System["./../../system/System.dnh"]

#include "./../../lib/lib_Boss.dnh"

int _objBoss;
int _diff = GetDifficulty() - 4;
int _count = -60;
int _loopTime = 5;

@Event {
    alternative (GetEventType())
    case (EV_REQUEST_LIFE) {
        SetScriptResult(2650); 
    }
    case (EV_REQUEST_TIMER) {
        SetScriptResult(60 + absolute(_count / 60));
    }
}

@Initialize {
    Boss_Init(true);
    _objBoss = Boss_Create(BOSS_ID_TSURUBAMI);
    TInitialize();
    TFinalize();
    TMain();
    TMovement();
}

@MainLoop {
    Boss_Update(_objBoss);
    _count++;
    yield;
}

task TInitialize() {
    ObjMove_SetDestAtFrame(_objBoss, 30, 70, 30, LERP_DECELERATE);
    wait(60);
    Boss_SetVulnerable(_objBoss, true);
}

task TFinalize() {
    while (_bossAlive) yield;
    ObjMove_SetDestAtFrame(_objBoss, MID_X, 60, 60, LERP_DECELERATE);
    Animation_Charge(false);
    Boss_Finalize(_objBoss);
}

task TMain() {
    while (_count < -30) yield;
    while (_bossAlive) {
        Boss_SetIntersection(_objBoss, intersectionShot, intersectionPlayer);
        if (_count % _loopTime == 0 && _count >= 0) {
            async {
                TFire();
            }
        }
        yield;
    }
}

task TFire() {
    if (!_bossAlive) return;
    int amulet = CreateShotUA1(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), 3.8, _count * 5, SHOT_BLACK+CARD, 0, SHOT_BLACK+SPAWN_S+SIZE_16, SHOT_BLACK+CANCEL_FADE);
    ObjShot_SetSpellResist(amulet, true);
    Laser_WarningLine(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), _count * 5, 550, 15);
    CallSFX(SND_SHOT1);

    wait(20);
    while (!Obj_IsDeleted(amulet)) {
        if (!_bossAlive) return;
        real xPos = ObjMove_GetX(amulet);
        real yPos = ObjMove_GetY(amulet);
        real angleT = ObjMove_GetAngle(amulet);
        CreateShotUA2(xPos, yPos, 0, angleT + 15, 0.025, 2.0, SHOT_GRAY+TALISMAN_A, 0, SHOT_GRAY+CANCEL_BURST1);
        CreateShotUA2(xPos, yPos, 0, angleT - 15, 0.025, 2.0, SHOT_GRAY+TALISMAN_A, 0, SHOT_GRAY+CANCEL_BURST1);
        wait(18);
    }
}

task TMovement() {
    while (_count < 0) yield;
    while (_bossAlive) {
        real x = 0.5 + 0.5 * (-cos(_count));
        real y = 0.5 + 0.5 * (sin(_count));
        real xPos = Interpolate_Linear(30, MAX_X - 30, x);
        real yPos = Interpolate_Linear(40, 100, y);
        ObjMove_SetDestAtFrame(_objBoss, xPos, yPos, 1);
        yield;
    }
}