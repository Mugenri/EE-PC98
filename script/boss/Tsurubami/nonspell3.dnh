#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Nonspell 3"]
#System["./../../system/System.dnh"]

#include "./../../lib/lib_Boss.dnh"

int _objBoss;
int _diff = GetDifficulty() - D_EASYEX;
int _count = -60;
int _loopTime = [180, 180, 42][_diff];
_spellID = 12 + _diff;

@Event {
    alternative (GetEventType())
    case (EV_REQUEST_LIFE) {
        SetScriptResult(3000);
    }
    case (EV_REQUEST_TIMER) {
        SetScriptResult(40 + absolute(_count / 60));
    }
}

@Initialize {
    Boss_Init(true);
    _objBoss = Boss_Create(BOSS_ID_TSURUBAMI);
    TInitialize();
    TFinalize();
    TMain();
}

@MainLoop {
    Boss_Update(_objBoss);
    _count++;
    yield;
}

task TInitialize() {
    ObjMove_SetDestAtFrame(_objBoss, MID_X, 60, 30, LERP_DECELERATE);
    wait(60);
    Boss_SetVulnerable(_objBoss, true);
}

task TFinalize() {
    while (_bossAlive) yield;
    ObjMove_SetDestAtFrame(_objBoss, MID_X, [60, 60, MID_Y][_diff], 60, LERP_DECELERATE);
    Animation_Charge(_objBoss, false);
    Boss_Finalize(_objBoss);
}

task TMain() {
    while (_count < -30) yield;
    while (_bossAlive) {
        Boss_SetIntersection(_objBoss, _intersectionShot, _intersectionPlayer);
        if (_count % _loopTime == 0 && _count >= 0) {
            async {
                Animation_Charge(_objBoss, true);
                TFireA(1);
                wait(_loopTime / 3);
                Animation_Charge(_objBoss, false);
                wait(_loopTime / 6);
                Animation_Charge(_objBoss, true);
                TFireA(-1);
                wait(_loopTime / 3);
                Animation_Charge(_objBoss, false);
            }
        }
        yield;
    }
}

task TFireA(int dir_) {
    if (!_bossAlive) return;
    Primitive_ContractPolygon2D(_bossX, _bossY, 0, 100, 0, 30, 32, COLOR_GRAY, false, RP_STG_EFF_MID);
    wait(3);
    Particle_ContractRing(_bossX, _bossY, 0, 100, -90 * dir_, 30, 8, COLOR_GRAY_L);
    wait(3);
    Particle_ContractRing(_bossX, _bossY, 0, 100, -90 * dir_, 30, 8, COLOR_GRAY_L);
    wait(30);

    if (!_bossAlive) return;
    //ring of rotating amulets that spawn lines of bullets that move away from the boss
    float angleT = 90;
    float speed = [3.2, 3.2, 5.2][_diff];
    loop (12) {
        int amulet = CreateShotUA1(_bossX, _bossY, speed, angleT, SHOT_BLACK+CARD, 0, SHOT_BLACK+CANCEL_FADE);
        ObjShot_SetSpellResist(amulet, true);
        TCommands(amulet);
        angleT += 360 / 12;
    }
    CallSFX(SND_SHOT1);

    wait(35);
    if (!_bossAlive) return;

    //aimed circles
    speed = [7.2, 9, 14.5][_diff];
    int numShot = [48, 64, 72][_diff];
    angleT = GetAngleToPlayer(_objBoss) + [0, 0, 360 / numShot][_diff];
    for (int i = 0; i < numShot; i++) {
        int amulet2 = CreateShotUA1(_bossX, _bossY, speed, angleT, SHOT_GRAY+CARD, 0, SHOT_GRAY+SPAWN_S+SIZE_16, SHOT_GRAY+CANCEL_FADE);
        ObjMove_AddPatternA2(amulet2, 12, NO_CHANGE, NO_CHANGE, -1, 1, NO_CHANGE);
        angleT += 360 / numShot;
        if (i % 2 == 1) ObjShot_SetDeleteFrame(amulet2, 40);
    }
    CallSFX(SND_SHOT1);

    task TCommands(int parent_) {
        ObjMove_SetAngularVelocity(parent_, 1.8 * dir_);
        ObjMove_AddPatternA2(parent_, 45, NO_CHANGE, NO_CHANGE, NO_CHANGE, NO_CHANGE, 1 * dir_);
        ObjMove_AddPatternA2(parent_, 90, NO_CHANGE, NO_CHANGE, NO_CHANGE, NO_CHANGE, 0.2 * dir_);
        float bossX = _bossX;
        float bossY = _bossY;
        int delayTime = [128, 128, 64][_diff];
        int waitTime = [16, 8, 5][_diff];
        float accel = [0.01, 0.01, 0.04][_diff];
        float maxSpeed = [2, 2, 3][_diff];
        int shotNum = [3, 4, 8][_diff];

        wait(20);

        while (!Obj_IsDeleted(parent_) && _bossAlive) {
            loop (shotNum) {
                if (Obj_IsDeleted(parent_) || !_bossAlive) break;
                float pX = ObjMove_GetX(parent_);
                float pY = ObjMove_GetY(parent_);
                int shot = CreateShotUA1(pX, pY, 0, dottheta(pX, pY, bossX, bossY) + 180, SHOT_BLACK+BALL_S, 0, SHOT_BLACK+SPAWN_E+SIZE_16, SHOT_BLACK+CANCEL_FADE);
                ObjMove_AddPatternA2(shot, delayTime, NO_CHANGE, NO_CHANGE, 0.01, maxSpeed, NO_CHANGE);
                wait([2, 2, 1][_diff]);
            }
            delayTime -= 16;
            delayTime = max(delayTime, 16);
            wait(waitTime);
        }
    }
}