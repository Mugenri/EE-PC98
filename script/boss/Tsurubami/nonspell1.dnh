#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["nonspell1"]
#System["./../../system/System.dnh"]
#Background["./../../system/TestBG.dnh"]

#include "./../../lib/bullet/lib_TsuruShot.dnh"
#include "./../../lib/lib_Common.dnh"
#include "./tsuruAnime.dnh"
#include "./lib_Tsuru.dnh"
#include "./../../lib/lib_Shot.dnh"

int objBoss;
int count = -120;
int loopTime = 120;

@Event {
    alternative (GetEventType())
    case (EV_REQUEST_LIFE) {
        SetScriptResult(350);
    }
    case (EV_REQUEST_TIMER) {
        SetScriptResult(60);
    }
    case (EV_REQUEST_SPELL_SCORE) {
        SetScriptResult(1000000);
    }
}

@Initialize {
    objBoss = ObjEnemy_Create(OBJ_ENEMY_BOSS);
    ObjEnemy_Regist(objBoss);
    InitCommon(); //TODO change to initboss and a boss library
    ObjMove_SetPosition(objBoss, MID_X, 60);

    //TBombBarrier();

    TDrawBoss(objBoss);
    TFinalize;
}

@MainLoop {
    ObjEnemy_SetIntersectionCircleToShot(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 32);
    ObjEnemy_SetIntersectionCircleToPlayer(objBoss, ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 24);
    if (count % loopTime == 0 && count >= 0) {
        //TPatternA();
        async {
            TPatternB(1);
            wait(60);
            TPatternB(-1);
        }
    }
    count++;

    yield;
}

task TFinalize() {
    while (ObjEnemy_GetInfo(objBoss, INFO_LIFE) > 0) yield;
    Obj_Delete(objBoss);

    int score = 10;
    int scoreBonus = 10;
    int numBullets = 0;
    //Need to put in a library, not do this if the spell is timed out
    //Actual MS bullet cancel score progression
    for each (shot in GetShotIdInCircleA2(MID_X, MID_Y, 500, TARGET_ENEMY)) {
        numBullets++;
        Obj_SetValueI(shot, BULLET_SCORE, score);
        if (numBullets <= 35) {
            Obj_SetValueI(shot, BULLET_DROP_ITEM, true);
        }
        //ObjShot_ToItem(shot);
        score += scoreBonus;
        scoreBonus += 30;
        score = min(score, 12800);
    }
    //^^
    DeleteShotAll(TYPE_SHOT, TYPE_ITEM);
    SetAutoDeleteObject(true);
    CloseScript(GetOwnScriptID());
    return;
}

task TPatternA() {
    AnimeCharge();
    wait(20);
    if (ObjEnemy_GetInfo(objBoss, INFO_LIFE) <= 0) return;
    real angleT = rand(0, 360);
    loop (5) {
        int id = 1;
        loop (10) {
            int obj = CreateShotA1(ObjMove_GetX(objBoss), ObjMove_GetY(objBoss), 4, angleT, id, 20);
            angleT += 360/10;
            id++;
            yield;
        }
    }
    wait(10);
    AnimeCharge();
}

task TPatternB(int dir_) {
    real angleT = 0;
    loop (8) {
        real x = ObjMove_GetX(objBoss) + 30 * cos(angleT);
        real y = ObjMove_GetY(objBoss) + 30 * sin(angleT);
        int obj = CreateStraightLaserU1(x, y, dottheta(x, y, GetPlayerX(), GetPlayerY()), 600, 60, 0xFF0000, 60);
        angleT += 45;
        TMove(obj);
    }

    task TMove(int laser_) {
        real x = ObjMove_GetX(laser_);
        loop (50) {
            ObjMove_SetX(laser_, x);
            ObjStLaser_SetAngle(laser_, GetAngleToPlayer(laser_));
            x += 3 * dir_;
            yield;
        }
        loop (70) {
            ObjMove_SetX(laser_, x);
            x += 1.5 * dir_;
            yield;
        }
    }
    
}