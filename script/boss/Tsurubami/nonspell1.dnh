#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["nonspell1"]
#System["./../../system/System.dnh"]

#include "./../../lib/lib_Boss.dnh"

int _objBoss;
int _diff = GetDifficulty() - D_EASYEX;
int _count = -60;
int _loopTime = 240;
_spellID = 0 + _diff;

@Event {
    alternative (GetEventType())
    case (EV_REQUEST_LIFE) {
        SetScriptResult(3000);
    }
    case (EV_REQUEST_TIMER) {
        SetScriptResult(40 + absolute(_count / 60));
    }
}

@Initialize {
    Boss_Init(true);
    if (GetGameMode() == GAME_MAIN) _objBoss = Boss_Create(BOSS_ID_TSURUBAMI, MID_X, 60);
    else _objBoss = Boss_Create(BOSS_ID_TSURUBAMI);
    TInitialize();
    TFinalize();
    TMain();
}

@MainLoop {
    Boss_Update(_objBoss);
    _count++;
    yield;
}

task TInitialize() {
    ObjMove_SetDestAtFrame(_objBoss, MID_X, 60, 30, LERP_DECELERATE);
    wait(60);
    Boss_SetVulnerable(_objBoss, true);
}

task TFinalize() {
    while (_bossAlive) yield;
    Animation_Charge(_objBoss, false);
    Boss_Finalize(_objBoss);
}

task TMain() {
    while (_count < -30) yield;
    while (_bossAlive) {
        Boss_SetIntersection(_objBoss, _intersectionShot, _intersectionPlayer);
        if (_count % _loopTime == 0 && _count >= 0) {
            async {
                Animation_Charge(_objBoss, true);
                TFireA(1);
                wait(60);
                TFireA(-1);
                wait(60);
                TFireB();
                wait(60);
                Animation_Charge(_objBoss, false);
            }
        }
        yield;
    }
}

task TFireA(int dir_) {
    if (!_bossAlive) return;
    Primitive_ContractPolygon2D(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), 45, 100, 0, 30, 32, COLOR_WHITE, false, RP_STG_EFF_MID);
    wait(3);
    Particle_ContractRing(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), 0, 100, 90 * dir_, 30, 8, COLOR_GRAY_L);
    wait(30);

    if (!_bossAlive) return;
    int amulet = CreateShotUA1(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), 5, 90 + 50 * dir_, SHOT_BLACK+CARD, 0, SHOT_BLACK+SPAWN_S+SIZE_16, SHOT_BLACK+CANCEL_FADE);
    ObjShot_SetSpellResist(amulet, true);
    ObjMove_SetAngularVelocity(amulet, -0.4 * dir_);
    CallSFX(SND_SHOT3);

    wait(20);
    int spawnNum = [8, 12, 24][_diff];
    int waitTime = [9, 6, 3][_diff];
    for (int i = 0; i < spawnNum; i++) {
        if (!_bossAlive || Obj_IsDeleted(amulet)) return;
        float x = ObjMove_GetX(amulet);
        float y = ObjMove_GetY(amulet);
        if (i % 2 == 0) TShotA(x, y);
        else TShotB(x, y);
        CallSFX(SND_SHOT1);
        wait(waitTime);
    }

    task TShotA(float x_, float y_) { //exploding ring
        int objShotA = CreateShotUA1(x_, y_, 0, 0, SHOT_BLACK+BALL_S, 0, SHOT_BLACK+SPAWN_E+SIZE_16, SHOT_BLACK+CANCEL_FADE);
        wait(60);
        if (!_bossAlive || Obj_IsDeleted(objShotA)) return;
        float x = ObjMove_GetX(objShotA);
        float y = ObjMove_GetY(objShotA);
        float angle = rand(0, 360);
        if (!Obj_IsDeleted(objShotA)) Obj_Delete(objShotA);

        loop (7) { //ring of bullets
            float speedMod = rand (-0.2, 0.2);
            CreateShotUA1(x, y, 0.9 + speedMod, angle, SHOT_GRAY+PILL_S, 0, SHOT_GRAY+SPAWN_S+SIZE_16, SHOT_GRAY+CANCEL_FADE);
            angle += 360 / 7;
        }
    }

    task TShotB(float x_, float y_) { //x way split
        float angle = 0;
        loop (5) {
            int obj = CreateShotUA1(x_, y_, 0, ObjMove_GetAngle(amulet) + angle, SHOT_BLACK+BALL_S, 0, SHOT_BLACK+SPAWN_E+SIZE_16, SHOT_BLACK+CANCEL_FADE);
            ObjMove_AddPatternA1(obj, 60, 1.3, NO_CHANGE);
            angle += 360 / 5;
        }
    }
}

task TFireB() {
    if (!_bossAlive) return;
    Primitive_ContractPolygon2D(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), 45, 100, 0, 30, 32, COLOR_WHITE, false, RP_STG_EFF_MID);
    wait(3);
    Particle_ContractRing(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), 15, 100, 90, 30, 8, COLOR_GRAY_L);
    Particle_ContractRing(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), -15, 100, -90, 30, 8, COLOR_GRAY_L);
    wait(30);

    if (!_bossAlive) return;
    int dir = 1;
    loop (2) {
        int amulet = CreateShotUA1(ObjMove_GetX(_objBoss), ObjMove_GetY(_objBoss), 5, 90 - 80 * dir, SHOT_BLACK+CARD, 0, SHOT_BLACK+SPAWN_S+SIZE_16, SHOT_BLACK+CANCEL_FADE);
        ObjShot_SetSpellResist(amulet, true);
        ObjMove_SetAngularVelocity(amulet, 4 * dir);
        ObjMove_AddPatternA2(amulet, 60, NO_CHANGE, NO_CHANGE, NO_CHANGE, NO_CHANGE, 0);
        TGravityShot(amulet);
        dir = -1;
    }
    CallSFX(SND_SHOT3);

    task TGravityShot(int obj_) {
        wait(20);
        int waitTime = [12, 8, 6][_diff];
        loop {
            if (!_bossAlive || Obj_IsDeleted(obj_)) return;
            loop (2) {
                CreateShotUB2(ObjMove_GetX(obj_), ObjMove_GetY(obj_), rand(-0.35, 0.35), -0.5, 0, 0.02 + rand(-0.01, 0.01), 0, 1.2, SHOT_BLACK+BALL_S, 0, SHOT_BLACK+SPAWN_S+SIZE_16, SHOT_BLACK+CANCEL_FADE);
            }
            CallSFX(SND_SHOT1);
            wait(waitTime);
        }
    }
}