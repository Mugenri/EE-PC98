#TouhouDanmakufu[Single]
#ScriptVersion[3]
#Title["Spell 5"]
#System["./../../system/System.dnh"]

#include "./../../lib/lib_Boss.dnh"

int _objClone; //second hitbox
int _objBoss;
int _diff = GetDifficulty() - D_EASYEX;
int _count = -360;
int _loopTime = [210, 210, 200][_diff];
_spellID = 27 + _diff;

@Event {
    alternative (GetEventType())
    case (EV_REQUEST_LIFE) {
        SetScriptResult([0, 3800]);
    }
    case (EV_REQUEST_TIMER) {
        SetScriptResult(50 + absolute(_count / 60));
    }
    case (EV_REQUEST_SPELL_SCORE) {
        SetScriptResult(1000000);
    }
}

@Initialize {
    Boss_Init(false);
    _objClone = Boss_Create(BOSS_ID_TSURUBAMI);
    _objBoss = Boss_Create(BOSS_ID_TSURUBAMI);
    ObjRender_SetColor(_objClone, COLOR_BLACK);
    ObjEnemyBossScene_StartSpell(_objScene);
    TInitialize();
    TFinalize();
    TMain();
    TBounds();
    TMovement(_objBoss, 1);
    TMovement(_objClone, -1);
}

@MainLoop {
    Boss_Update(_objBoss);
    _count++;
    yield;
}

task TInitialize() {
    ObjMove_SetDestAtFrame(_objBoss, MID_X, MID_Y, 30, LERP_DECELERATE);
    ObjMove_SetDestAtFrame(_objClone, MID_X, MID_Y, 30, LERP_DECELERATE);
    wait(30);
    Transition_Rise(5, 0);
    CallSFX(SND_TRANSITION);
    wait(330);
    Boss_SetVulnerable(_objBoss, true);
}

task TFinalize() {
    while (_bossAlive) yield;
    Obj_Delete(_objClone);
    ObjMove_SetDestAtFrame(_objBoss, MID_X, 60, 60, LERP_DECELERATE);
    Animation_Charge(_objBoss, false);
    Boss_Finalize(_objBoss);
    Transition_ChangeBG(0, 0);
}

task TMain() {
    wait(150);
    TSplit();
    while (_count < -30) yield;
    while (_bossAlive) {
        Boss_SetIntersection(_objBoss, _intersectionShot, 8);
        Boss_SetIntersection(_objBoss, _objClone, _intersectionShot, 8);
        if (_count % _loopTime == 0 && _count >= 0) {
            async {
                wait(90);
                TFire();
            }
        }
        yield;
    }
}

task TMovement(int obj_, int dir_) {
    while (_count < 0) yield;
    while (_bossAlive) {
        float x = 0.5 + 0.5 * (-cos(_count));
        float y = 0.5 + 0.5 * (sin(_count));
        float xPos = Interpolate_Linear(MID_X - 150 * dir_, MID_X + 150 * dir_, x);
        float yPos = Interpolate_Linear(MID_Y + 150 * dir_, MID_Y - 150 * dir_, y);
        ObjMove_SetDestAtFrame(obj_, xPos, yPos, 1);
        yield;
    }
}

task TBounds() {
    while (_count < 0) yield;
    int waitTime = [10, 6, 5][_diff];
    while (_bossAlive) {
        float x1 = _bossX;
        float y1 = _bossY;
        float angleT = dottheta(MID_X, MID_Y, x1, y1);
        float x2 = MID_X + 150 * cos(angleT + 180);
        float y2 = MID_Y + 150 * sin(angleT + 180);
        CreateShotUA1(x1, y1, 1.0, angleT, SHOT_BLACK+TALISMAN_A, 0, SHOT_BLACK+SPAWN_E+SIZE_16, SHOT_BLACK+CANCEL_BURST2);
        CreateShotUA1(x1, y1, 0.5, angleT + 12, SHOT_BLACK+TALISMAN_A, 2, SHOT_BLACK+SPAWN_E+SIZE_16, SHOT_BLACK+CANCEL_BURST2);
        CreateShotUA1(x1, y1, 0.2, angleT + 90, SHOT_BLACK+TALISMAN_A, 4, SHOT_BLACK+SPAWN_E+SIZE_16, SHOT_BLACK+CANCEL_BURST2);
        CreateShotUA1(x2, y2, 1.0, angleT + 180, SHOT_GRAY+TALISMAN_A, 0, SHOT_GRAY+SPAWN_E+SIZE_16, SHOT_GRAY+CANCEL_BURST2);
        CreateShotUA1(x2, y2, 0.5, angleT + 12 + 180, SHOT_GRAY+TALISMAN_A, 2, SHOT_GRAY+SPAWN_E+SIZE_16, SHOT_GRAY+CANCEL_BURST2);
        CreateShotUA1(x2, y2, 0.2, angleT + 90 + 180, SHOT_GRAY+TALISMAN_A, 4, SHOT_GRAY+SPAWN_E+SIZE_16, SHOT_GRAY+CANCEL_BURST2);
        CallSFX(SND_SHOT1);
        wait(waitTime);
    }
}

task TSplit() {
    Animation_Charge(_objBoss, true);
    CallSFX(SND_CHARGE1);

    loop (2) {
        Primitive_ContractPolygon2D(_bossX, _bossY, 0, 350, 0, 100, 32, COLOR_BLACK, false, RP_STG_EFF_MID);
        wait(2);
    }
    loop (3) {
        Primitive_ContractPolygon2D(_bossX, _bossY, 0, 350, 0, 100, 32, COLOR_GRAY_D, false, RP_STG_EFF_MID);
        wait(2);
    }
    loop (3) {
        Primitive_ContractPolygon2D(_bossX, _bossY, 0, 350, 0, 100, 32, COLOR_GRAY_L, false, RP_STG_EFF_MID);
        wait(2);
    }
    loop (2) {
        Primitive_ContractPolygon2D(_bossX, _bossY, 0, 350, 0, 100, 32, COLOR_WHITE, false, RP_STG_EFF_MID);
        wait(2);
    }
    wait(100);

    CallSFX(SND_SHOT2);
    Animation_Charge(_objBoss, false);

    ObjMove_SetDestAtFrame(_objBoss, MID_X - 150, MID_Y, 60, LERP_DECELERATE);
    ObjMove_SetDestAtFrame(_objClone, MID_X + 150, MID_Y, 60, LERP_DECELERATE);
}

task TFire() {
    if (!_bossAlive) return;

    int[] shots = [];
    
    //tsuru burst fire
    float radius = rand(0, 5);
    float angleT = ObjMove_GetAngle(_objBoss) + 90;
    int waitTime = [0, 0, 1][_diff];
    int numShot = [8, 12, 64][_diff];
    int moveTime = [125, 110, 50][_diff];
    int trailNum = [2, 4, 1] [_diff];
    loop (numShot) {
        if (!_bossAlive) return;
        float radius = 150;
        float xPos = MID_X + radius * cos(angleT);
        float yPos = MID_Y + radius * sin(angleT);

        int delay = 3;
        loop (trailNum) {
            int shot2 = CreateShotUA1(_bossX, _bossY, 0, 0, SHOT_GRAY_INV+CRYSTAL_S, 0, SHOT_GRAY_INV+CANCEL_SPARKLE);
            ObjMove_AddPatternD2(shot2, delay, xPos, yPos, moveTime, LERP_ACCELERATE);
            ObjShot_SetDeleteFrame(shot2, moveTime + delay - 1);
            delay += 3;
        }

        int shot = CreateShotUA1(_bossX, _bossY, 0, 0, SHOT_GRAY+PILL_A, 0, SHOT_GRAY+CANCEL_32);
        ObjMove_AddPatternD2(shot, 0, xPos, yPos, moveTime, LERP_ACCELERATE);
        ObjShot_SetSpellResist(shot, true);
        shots ~= [shot];
        CallSFX(SND_SHOT3);
        angleT -= 360 / numShot;
        wait(waitTime);
    }

    wait([80, 80, 30][_diff]);
    if (!_bossAlive) return;

    //clone lasers
    int sustain = [30, 45, 10][_diff];
    int delay = [70, 70, 50][_diff];
    for each (shot in shots) { //create straight laser for each shot
        TLaser(shot, sustain, delay);
        wait([0, 0, 1][_diff]);
    }

    task TLaser(int shot_, int sustain_, int delay_) { //draw laser from boss to shot
        float bX = ObjMove_GetX(_objClone);
        float bY = ObjMove_GetY(_objClone);
        float sX = ObjMove_GetX(shot_);
        float sY = ObjMove_GetY(shot_);
        float angleT = dottheta(bX, bY, sX, sY);
        int laser = CreateStraightLaserUA1(bX, bY, angleT, 500, sustain_, COLOR_GRAY, delay_);

        async {
            loop (sustain_ + delay_) {
                if (!_bossAlive || Obj_IsDeleted(laser)) return;
                bX = ObjMove_GetX(_objClone);
                bY = ObjMove_GetY(_objClone);
                sX = ObjMove_GetX(shot_);
                sY = ObjMove_GetY(shot_);
                angleT = dottheta(bX, bY, sX, sY);
                ObjMove_SetPosition(laser, bX, bY);
                ObjStLaser_SetAngle(laser, angleT);
                yield;
            }
        }

        if (Obj_IsDeleted(shot_) || !_bossAlive) return;
        wait(delay_); //laser expand animation
        if (Obj_IsDeleted(shot_) || !_bossAlive) return;
        CallSFX(SND_LASER1);
        wait(sustain_);
        ObjShot_SetDeleteFrame(shot_, 0);
    }
}